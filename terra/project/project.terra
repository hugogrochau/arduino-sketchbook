#include "/home/terra/TerraNG/terra/TerraNet.defs"
#define BROADCAST_TYPE 1
#define DATA_TYPE 2

var ushort nodeId = getNodeId();

pktype usrMsg from radioMsg with
    var ubyte[4]  d8;
    var ushort[4] d16;
    var ulong[2]  d32;
end

var usrMsg sndData;
var usrMsg recData;
var usrMsg data;


sndData.source = nodeId;
sndData.type = BROADCAST_TYPE;
sndData.target = BROADCAST;
sndData.d16[2] = 0;

if nodeId == 11 then
    emit SEND(sndData);
    await SEND_DONE;
    sndData.target = 1;
else
    recData = await RECEIVE;
    var ushort parent = recData.source;
    sndData.d16[3] = recData.d16[3] + 1;
    emit SEND(sndData);
    await SEND_DONE;
    sndData.target = parent;
end

sndData.type = DATA_TYPE;

par do
    var ushort randNum = (random() % 1000) + (1000 * sndData.d16[3]);
    await (randNum)ms;
    loop do
        emit REQ_TEMP();
        sndData.d16[0] = await TEMP;
        sndData.d16[1] = nodeId;
        sndData.d16[2] = sndData.d16[2] + 1;

        qPut(sndData);

        emit LED0(TOGGLE);
        await 18s;
    end
with
    loop do
        recData = await RECEIVE(DATA_TYPE);
        recData.target = sndData.target;

        qPut(recData);

        emit LED2(TOGGLE);
    end
with
    loop do
        if qSize() == 0 then
            await Q_READY;
        else
            qGet(data);
            emit SEND_ACK(data);
            await SEND_DONE_ACK;
            await 250ms;
        end
    end
end
